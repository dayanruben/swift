// RUN: %target-sil-opt -test-runner %s 2>&1 | %FileCheck %s

import Builtin

class Klass {}

sil @getKlass : $@convention(thin) () -> @owned Klass
sil @useIndirect : $@convention(thin) <T> (@in_guaranteed T) -> ()

// CHECK-LABEL: begin running test {{[0-9]+}} of {{[0-9]+}} on simple_test_case: variable-name-inference with: @trace[0]
// CHECK: Input Value:   %1 = apply %0() : $@convention(thin) () -> @owned Klass
// CHECK: Name: 'MyName'
// CHECK: Root:   %1 = apply %0() : $@convention(thin) () -> @owned Klass
// CHECK: end running test {{[0-9]+}} of {{[0-9]+}} on simple_test_case: variable-name-inference with: @trace[0]
sil [ossa] @simple_test_case : $@convention(thin) () -> () {
bb0:
  specify_test "variable-name-inference @trace[0]"
  %0 = function_ref @getKlass : $@convention(thin) () -> @owned Klass
  %1 = apply %0() : $@convention(thin) () -> @owned Klass
  debug_value [trace] %1 : $Klass
  debug_value %1 : $Klass, let, name "MyName"
  destroy_value %1 : $Klass
  %9999 = tuple ()
  return %9999 : $()
}

// CHECK-LABEL: begin running test {{[0-9]+}} of {{[0-9]+}} on temporary_init_with_copy_addr: variable-name-inference with: @trace[0]
// CHECK: Input Value:   %4 = alloc_stack $Klass
// CHECK: Name: 'MyName'
// CHECK: Root:   %2 = alloc_stack $Klass, var, name "MyName"
// CHECK: end running test {{[0-9]+}} of {{[0-9]+}} on temporary_init_with_copy_addr: variable-name-inference with: @trace[0]
sil [ossa] @temporary_init_with_copy_addr : $@convention(thin) () -> () {
bb0:
  specify_test "variable-name-inference @trace[0]"
  %0 = function_ref @getKlass : $@convention(thin) () -> @owned Klass
  %1 = apply %0() : $@convention(thin) () -> @owned Klass
  %2 = alloc_stack $Klass, name "MyName"
  store %1 to [init] %2 : $*Klass

  %temp = alloc_stack $Klass
  copy_addr %2 to [init] %temp : $*Klass
  debug_value [trace] %temp : $*Klass
  %use = function_ref @useIndirect : $@convention(thin) <T> (@in_guaranteed T) -> ()
  apply %use<Klass>(%temp) : $@convention(thin) <T> (@in_guaranteed T) -> ()

  destroy_addr %temp : $*Klass
  dealloc_stack %temp : $*Klass
  destroy_addr %2 : $*Klass
  dealloc_stack %2 : $*Klass
  %9999 = tuple ()
  return %9999 : $()
}

// CHECK-LABEL: begin running test {{[0-9]+}} of {{[0-9]+}} on temporary_init_with_store: variable-name-inference with: @trace[0]
// CHECK: Input Value:   %3 = alloc_stack $Klass
// CHECK: Name: 'MyName'
// CHECK: Root:   %1 = apply %0()
// CHECK: end running test {{[0-9]+}} of {{[0-9]+}} on temporary_init_with_store: variable-name-inference with: @trace[0]
sil [ossa] @temporary_init_with_store : $@convention(thin) () -> () {
bb0:
  specify_test "variable-name-inference @trace[0]"
  %0 = function_ref @getKlass : $@convention(thin) () -> @owned Klass
  %1 = apply %0() : $@convention(thin) () -> @owned Klass
  debug_value %1 : $Klass, name "MyName", let

  %temp = alloc_stack $Klass
  store %1 to [init] %temp : $*Klass
  debug_value [trace] %temp : $*Klass
  %use = function_ref @useIndirect : $@convention(thin) <T> (@in_guaranteed T) -> ()
  apply %use<Klass>(%temp) : $@convention(thin) <T> (@in_guaranteed T) -> ()

  destroy_addr %temp : $*Klass
  dealloc_stack %temp : $*Klass
  %9999 = tuple ()
  return %9999 : $()
}
